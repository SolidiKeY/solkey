\sorts {
    Memory;
    java.lang.Identity \extends java.lang.Object;
    IdentityPrim;
    Field;

	\generic alpha \extends any;

	Struct \extends any;
	List;
}


\functions{
    // Core
    \unique List mt;
    \unique List cons(any, List);
    List consr(List, any);

    alpha alpha::head(List);
    List tail(List);

    List firsts(List);
    alpha alpha::last(List);

    // Memory
    \unique Memory mtMem;

    Memory write(Memory, Identity, Field, any);
    Memory addM(Memory, IdentityPrim);
    alpha alpha::read(Memory, Identity, Field);
    alpha alpha::readR(Memory, Identity, List);

    \unique Identity idC(IdentityPrim, List);
    any default(Identity, Field);
    any defaultF(Field);

    // Storage
    \unique Struct mtSt;

    Struct storeSt(Struct, Field, any);
    Struct save(Struct, List, any);
    alpha alpha::selectSt(Struct, Field);
    alpha alpha::find(Struct, List);

    \unique Field size;

    // Memory to Storage
    Struct copyMem(Struct, Memory, Identity);

    // Storage to Memory
    Memory copySt(Memory, IdentityPrim, any);
}

\predicates {
    new(Memory, IdentityPrim);
    isPrimitive(Field);
    sizeLE1(List);
    isEmpty(List);
}

\schemaVariables{
    \term Memory mem;
    \term Identity id, id1, id2;
    \term IdentityPrim idp1, idp2;
    \term Field fld, fld1, fld2;
    \term List fxs, flds, xs;
    \term any x, y, v, v0, v1;
    \term Memory mem;
    \term Struct st;
}

\programVariables {
}

\rules {
    // Core
    headDefinition {
        \find(alpha::head(cons(x, xs)))
        \replacewith(x)
        \heuristics(simplify)
    };

    tailDefinition {
        \find(tail(cons(x, xs)))
        \replacewith(xs)
        \heuristics(simplify)
    };

    lastConsMt {
        \find(alpha::last(cons(x, mt)))
        \replacewith(x)
        \heuristics(simplify)
    };

    lastConsRest {
        \find(alpha::last(cons(x, cons(y, xs))))
        \replacewith(alpha::last(cons(y, xs)))
        \heuristics(simplify)
    };

    firstsConsMt {
        \find(firsts(cons(x, mt)))
        \replacewith(mt)
        \heuristics(simplify)
    };

    firstsConsRest {
        \find(firsts(cons(x, cons(y, xs))))
        \replacewith(cons(x, firsts(cons(y, xs))))
        \heuristics(simplify)
    };

    isEmptyEmpty {
        \find(isEmpty(mt))
        \replacewith(true)
        \heuristics(concrete)
    };

    isEmptyCons {
        \find(isEmpty(cons(x, xs)))
        \replacewith(false)
        \heuristics(concrete)
    };

    sizeLE1Empty {
        \find(sizeLE1(mt))
        \replacewith(true)
        \heuristics(concrete)
    };

    sizeLE1Cons {
        \find(sizeLE1(cons(x, mt)))
        \replacewith(true)
        \heuristics(concrete)
    };

    sizeLE1ConsCons {
        \find(sizeLE1(cons(x, cons(y, xs))))
        \replacewith(false)
        \heuristics(concrete)
    };

    consREmpty {
        \find(consr(mt, x))
        \replacewith(cons(x,mt))
        \heuristics(simplify)
    };

    consRcons {
        \find(consr(cons(x, xs), y))
        \replacewith(cons(x, consr(xs, y)))
        \heuristics(simplify)
    };

    // Memory
    readOnWrite {
        \find(alpha::read(write(mem, id1, fld1, v), id2, fld2))
        \replacewith( \if ( id1 = id2 & fld1 = fld2)
                      \then (v)
                      \else (alpha::read(mem, id2, fld2))
                    )
        \heuristics(simplify_enlarging)
    };

    readOnAddM {
        \find(alpha::read(addM(mem, idp1), idC(idp2, flds), fld))
            \replacewith(
                \if (idp1 = idp2) \then (
                        default(idC(idp2, flds), fld)
                ) \else (
                        alpha::read(mem, idC(idp2, flds), fld)
                )
        )
        \heuristics(simplify_enlarging)
    };

    readFromEmptyMemory {
        \find(alpha::read(mtMem, id, fld))
        \replacewith(default(id, fld))
        \heuristics(simplify)
    };

    readREmpty {
        \find(alpha::readR(mem, id, cons(fld, mt)))
        \replacewith( alpha::read(mem, id, fld) )
        \heuristics(simplify)
    };

    readRCons {
        \find(alpha::readR(mem, id, cons(fld1, cons(fld2, flds))))
        \replacewith(
            alpha::read(mem, Identity::readR(mem, id, firsts(cons(fld1, cons(fld2, flds)))),
               Field::last(cons(fld1, cons(fld2, flds))))
        )
        \heuristics(simplify)
    };

    newFromAdd {
        \find(new(addM(mem, idp1), idp2))
        \replacewith(
            \if (idp1 = idp2)
            \then (false)
            \else (new(mem, idp2))
        )
        \heuristics(simplify_enlarging)
    };

    newFromWrite {
        \find(new(write(mem, id1, fld1, v), idp2))
        \replacewith(new(mem, idp2))
        \heuristics(simplify_enlarging)
    };

    newFromEmptyMemory {
        \find(new(mtMem, idp1))
        \replacewith(true)
        \heuristics(simplify_enlarging)
    };

    // Storage
    selectOnStore {
        \find(alpha::selectSt(storeSt(st, fld1, v), fld2))
        \replacewith(
            \if (fld1 = fld2)
            \then (v)
            \else (alpha::selectSt(st,fld2))
        )
        \heuristics(simplify_enlarging)
    };

    selectOnEmptyStorage {
        \find(alpha::selectSt(mtSt, fld))
        \replacewith(alpha::defaultF(fld) )
        \heuristics(simplify_enlarging)
    };

    saveOnEmptyStorageEmpty {
        \find(save(mtSt, mt, v))
        \replacewith(v)
        \heuristics(simplify_enlarging)
    };

    saveOnEmptyStorage {
        \find(save(mtSt,cons(fld, flds), v))
        \replacewith( storeSt(mtSt, fld, save(mtSt, flds, v)) )
        \heuristics(simplify_enlarging)
    };

    saveOnStoreEmpty {
        \find(save(storeSt(st, fld, v0), mt, v1))
        \replacewith(v1)
        \heuristics(simplify_enlarging)
    };

    saveOnStoreCons {
        \find(save(storeSt(st, fld1, v0), cons(fld2, flds), v1))
        \replacewith(
            \if (fld1 = fld2)
            \then ( storeSt(st, fld1,
                \if ( isEmpty(flds) )
                \then ( v1 )
                \else ( save( (Struct)v0, flds, v1) )
            ))
            \else ( storeSt(save(st, flds, v1), fld1, v0) )
        )
        \heuristics(simplify_enlarging)
    };

    findDefinitionEmpty {
        \find(alpha::find(st, mt))
        \replacewith( (alpha) st )
        \heuristics(simplify_enlarging)
    };

    findDefinitionCons {
        \find(alpha::find(st, cons(fld, flds)))
        \replacewith(
            \if(isEmpty(flds))
            \then( alpha::selectSt(st, fld) )
            \else( alpha::find(Struct::selectSt(st, fld), flds) )
        )
        \heuristics(simplify_enlarging)
    };

    // lazy
    saveOnEmpty {
        \find(save(st, mt, v))
        \replacewith(v)
        \heuristics(simplify)
    };

    selectOnSaveEmpty {
        \find(alpha::selectSt(save(st,mt,v), fld))
        \replacewith(alpha::selectSt(save(st,flds,v), fld))
        \heuristics(simplify)
    };

    selectOnSaveCons {
        \find(alpha::selectSt(save(st,cons(fld, flds),v), fld))
        \replacewith(
            (alpha) save(Struct::selectSt(st,fld), flds, v)
        )
        \heuristics(simplify)
    };

    // Memory To Storage

    findOnCopy {
        \find(alpha::find(copyMem(st, mem, id), flds))
        \replacewith(
            \if (!isEmpty(flds) & isPrimitive(Field::last(flds)))
            \then (alpha::readR(mem, id, flds))
            \else (alpha::find(copyMem(st, mem, id), flds))
        )
        \heuristics(simplify_enlarging)
    };

    // Storage to Memory

    readFromCopyToStorage {
        \find( alpha::read(copySt(mem, idp1, st), idC(idp2, fxs), fld) )
        \replacewith (
            \if (idp1 = idp2)
            \then (
                \if ( isPrimitive(fld) )
                \then ( alpha::find(st, consr(fxs, fld)) )
                \else ( idC(idp1, consr(fxs, fld)) )
            )
            \else ( alpha::read(mem, idC(idp2, fxs), fld) )
        )
        \heuristics(simplify_enlarging)
    };
}

\functions {
    \unique Field f1;
    \unique Field f2;
    \unique Field f3;
    \unique IdentityPrim idp1;
    \unique IdentityPrim idp2;
    \unique Identity id1;
    \unique Identity id2;
}

\programVariables {
    Memory mem;
    Struct st;
    List s, flds, flds12;
    alpha v;
    Struct cSt;
}

\rules {
    fldsExDef {
        \find(flds12)
        \replacewith(cons(f1, cons(f2, mt)))
        \heuristics(concrete)
    };

    primitiveF1 {
        \find(isPrimitive(f1))
        \replacewith(true)
        \heuristics(concrete)
    };

    primitiveF2 {
        \find(isPrimitive(f2))
        \replacewith(true)
        \heuristics(concrete)
    };

    notPrimitiveF3 {
        \find(isPrimitive(f3))
        \replacewith(false)
        \heuristics(concrete)
    };
}

\problem {
    // isPrimitive(f1) & isPrimitive(f2) & !isPrimitive(f3) ->
    // f1 != f2 & idp1 != idp2 & isPrimitive(f1) & isPrimitive(f2) & !isPrimitive(f3) ->
    true

    // int::read(write(mem, id1, f1, 0), id1, f1) = 0
    // int::read(addM(mem, idp1), idC(idp2, flds), f1) = int::read(mem, idC(idp2, flds), f1)
    // int::read(mtMem, id1, f1) = default(id1, f1)
    // int::selectSt(storeSt(st, f1, 0), f1) = 0
    // int::selectSt(storeSt(st, f1, 0), f2) = int::selectSt(st, f2)
    // int::selectSt(mtSt, f1) = alpha::defaultF(f1)
    // ! new(addM(mem, idp1), idp1)
    // new(mem, idp2) -> new(write(mem, id1, f1, 0), idp2)
    // new(mtMem, idp1)
    // save(mtSt, flds12, 0) = storeSt(mtSt, f1, storeSt(mtSt, f2, 0))
    // (int)save(mtSt, mt, 0) = 0
    // save(storeSt(mtSt, f1, 0), cons(f1, mt), 1) = storeSt(mtSt, f1, 1)
    // save(storeSt(mtSt, f2, 0), cons(f2, mt), 1) = storeSt(mtSt, f2, 1)
    // save(storeSt(mtSt, f1, storeSt(mtSt, f2, 0)), flds12, 1) = storeSt(mtSt, f1, storeSt(mtSt, f2, 1))
    // int::find(storeSt(mtSt, f1, 0), cons(f1, mt)) = 0
    // int::find(storeSt(mtSt, f1, storeSt(mtSt, f2, 0)), flds12) = 0
    // int::selectSt(save(st, cons(f1, mt), 0), f1) = 0
    // int::find(save(st, flds12, 0), flds12) = 0
    // int::readR(write(mem, id1, f1, 0), id1, cons(f1, mt)) = 0
    // int::readR(write(write(mem, id2, f2, 0), id1, f1, id2), id1, flds12) = 0
    // int::find(copyMem(st, write(write(mem, id2, f2, 0), id1, f1, id2), id1), flds12) = 0
    // int::read(copySt(mem, idp1, mtSt), idC(idp2, mt), f1) = int::read(mem, idC(idp2, mt), f1)
    // int::read(copySt(mem, idp1, storeSt(mtSt, f1, 1)), idC(idp1, mt), f1) = 1
    // Identity::read(copySt(mem, idp1, storeSt(mtSt, f1, 1)), idC(idp1, mt), f3) = idC(idp1, cons(f3, mt))
    // idC(idp1, flds12) != idC(idp1, cons(f2, cons(f1, mt)))
    // int::head(cons(0, mt)) = 0
}

